buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.akhikhl.gretty:gretty:2.0.0'
    }
}

apply plugin: 'base'
apply plugin: 'org.akhikhl.gretty'

repositories {
    mavenCentral()
    maven { url "http://smartbearsoftware.com/repository/maven2/" }
}


configurations {
    soapui {
        resolutionStrategy {
            dependencySubstitution {
                substitute module('javax.jms:jms') with module('javax.jms:jms-api:1.1-rev-1')
            }
        }
    }

    war
}



dependencies {
    soapui(group: 'com.smartbear.soapui', name: 'soapui', version: '5.3.0') {
//        exclude(module: 'jtidy')
//        exclude(module: 'cajo')
    }

}

task copyDependencies(type: Copy) {

    def filesToCopy = copySpec {
        from configurations.soapui
    }

    into buildDir

    into("lib") {
        exclude "soapui-*.jar"
        with filesToCopy
    }
    into("soapui") {
        include "soapui-*.jar"
        with filesToCopy
    }
}

task createWar(type: JavaExec, group: 'build') {
    dependsOn copyDependencies

    inputs.file file('SoapUI-War-soapui-project.xml')

    systemProperties = ["soapui.home": "$buildDir/soapui",
                        "user.home"  : projectDir]

    classpath = configurations.soapui

    main = 'com.eviware.soapui.tools.SoapUIMockAsWarGenerator'

    args "-d", "$buildDir/war", "-f", "$buildDir/soapuimocks.war", "-w", "true", "-s", "none.xml", file('SoapUI-War-soapui-project.xml')

    ext.destFile = file("$buildDir/soapuimocks.war")

    outputs.file destFile
}

task runSoapUi(type: JavaExec, group: 'run') {
    dependsOn copyDependencies

    inputs.file file('SoapUI-War-soapui-project.xml')

    systemProperties = ["soapui.home": "$buildDir/soapui",
                        "user.home"  : projectDir]

    classpath = configurations.soapui

    main = 'com.eviware.soapui.SoapUI'

//    args file('SoapUI-War-soapui-project.xml')
}

build.dependsOn 'createWar'

artifacts {

    war(createWar.destFile) {
        name 'soaui-mock'
        type 'war'
        builtBy createWar
    }
}

gretty {
    servletContainer = 'jetty9'

    httpEnabled = true
    httpPort = 8081
    jvmArgs { '-ea' }

    loggingLevel = 'INFO'

    systemProperties = ["soapui.home": "$buildDir/soapui",
                        "user.home"  : projectDir]
}

farm {
    webapp createWar.destFile, contextPath: "/"
}

project.afterEvaluate {
    tasks.farmRun.dependsOn createWar
}
